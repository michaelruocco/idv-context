plugins {
    id "com.github.johnrengelman.shadow" version "${shadowPluginVersion}"
    id "maven-publish"
}

apply from: rootProject.file('publish.gradle')

dependencies {
    api "com.github.stefanbirkner:system-lambda:${systemLambdaVersion}"
    api project(":context-client")
    api testFixtures(project(":context-entities"))
    api testFixtures(project(":otp-entities"))
    api testFixtures(project(":method-json"))
    api testFixtures(project(":context-json"))
    api testFixtures(project(":identity-json"))
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
}

java {
    withJavadocJar()
    withSourcesJar()
}

shadowJar {
    dependencies {
        exclude(project(':context-client'))
        exclude(dependency("javax.money:money-api"))
        exclude(dependency("org.javamoney.moneta:moneta-core"))
        exclude(dependency("org.javamoney.moneta:moneta-convert"))
        exclude(dependency("org.javamoney.moneta:moneta-convert-ecb"))
        exclude(dependency("org.javamoney.moneta:moneta-convert-imf"))
        exclude(dependency("org.slf4j:slf4j-simple"))
    }
}

publishing {
    publications {
        lib(MavenPublication) { publication ->
            project.shadow.component(publication)
            artifactId "${project.name}"
            pom.withXml {
                def root = asNode()
                root.appendNode("description", "Verification context client library test fixtures")
                root.appendNode("name", "${project.name}")
                root.appendNode("url", "https://github.com/michaelruocco/verification-context")
                root.children().last() + project.pomConfig
            }
        }
    }
}