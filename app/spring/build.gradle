buildscript {
    ext {
        springBootVersion = "2.4.2"
    }
}

plugins {
    id "org.unbroken-dome.test-sets" version "${testSetsPluginVersion}"
    id "org.springframework.boot" version "${springBootVersion}"
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
    id "com.bmuschko.docker-remote-api" version "6.7.0"
    id "com.avast.gradle.docker-compose" version "0.14.0"
    id "maven-publish"
}

ext {
    karateVersion = "0.9.6"
    logbackJsonVersion = "0.1.5"
    artifactName = "${rootProject.name}-${project.name}"
}

testSets {
    integrationTest
}

configurations {
    runtimeOnly.exclude module: "slf4j-simple"
}

repositories {
    maven { url "https://oss.jfrog.org/artifactory/oss-snapshot-local" }
}

dependencies {
    implementation project(":manual-app")
    implementation project(":identity-config")
    implementation project(":lockout-config")
    implementation project(":context-config")
    implementation project(":identity-json")
    implementation project(":method-config")
    implementation project(":otp-config")
    implementation project(":otp-sim-swap-stub-config")
    implementation project(":lockout-json")
    implementation project(":context-json")
    implementation project(":method-json")
    implementation project(":otp-json")
    implementation project(":identity-external-find-stub-config")
    implementation "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.hateoas:spring-hateoas"
    implementation "com.github.bohnman:squiggly-filter-jackson:1.3.18"
    implementation "com.github.michaelruocco:spring-filters:0.1.9"
    implementation "com.github.michaelruocco:string-utils:${mruocStringUtilsVersion}"
    implementation "com.github.michaelruocco:json-adapter:1.0.0"
    implementation "com.github.michaelruocco:testing-clocks:${testingClocksVersion}"
    implementation "com.fasterxml.jackson.core:jackson-databind"
    implementation "org.redisson:redisson:${redissionVersion}"
    implementation "org.mongodb:mongo-java-driver:${mongoVersion}"
    runtimeOnly "ch.qos.logback.contrib:logback-json-classic:${logbackJsonVersion}"
    runtimeOnly "ch.qos.logback.contrib:logback-jackson:${logbackJsonVersion}"
    runtimeOnly "net.logstash.logback:logstash-logback-encoder:6.6"

    testImplementation testFixtures(project(":lockout-entities"))
    testImplementation testFixtures(project(":context-entities"))

    integrationTestImplementation "org.junit-pioneer:junit-pioneer:${junitPioneerVersion}"
    integrationTestImplementation "com.intuit.karate:karate-core:${karateVersion}"
    integrationTestImplementation "com.intuit.karate:karate-junit5:${karateVersion}"
    integrationTestImplementation "org.awaitility:awaitility"
    integrationTestImplementation "org.testcontainers:junit-jupiter:1.15.1"
    integrationTestImplementation "junit:junit"
    integrationTestRuntimeOnly "com.intuit.karate:karate-apache:${karateVersion}"
}

springBoot {
    buildInfo()
}

bootRun {
    systemProperties = System.getProperties()
    systemProperty "spring.profiles.active", System.properties.getOrDefault("spring.profiles.active", "stubbed,simple-logging")
    systemProperty "server.port", System.properties.getOrDefault("server.port", "8081")
    systemProperty "request.logging.enabled", System.properties.getOrDefault("request.logging.enabled", "true")
    systemProperty "response.logging.enabled", System.properties.getOrDefault("response.logging.enabled", "true")
    systemProperty "response.filtering.enabled", System.properties.getOrDefault("response.filtering.enabled", "true")
}

integrationTest {
    systemProperty "aws.accessKeyId", "abc"
    systemProperty "aws.secretKey", "123"
}

apply from: rootProject.file('publish.gradle')

publishing {
    publications {
        lib(MavenPublication) { publication ->
            artifact bootJar
            artifact sourcesJar
            artifact javadocJar
            artifactId "${artifactName}"
            pom.withXml {
                def root = asNode()
                root.appendNode("description", "Verification context spring application")
                root.appendNode("name", "${artifactName}")
                root.appendNode("url", "https://github.com/michaelruocco/verification-context")
                root.children().last() + project.pomConfig
            }
        }
    }
}

docker {
    registryCredentials {
        username.set(System.getenv("DOCKER_USERNAME"))
        password.set(System.getenv("DOCKER_PASSWORD"))
    }
}

import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage

task buildImage(type: DockerBuildImage) {
    inputDir.set(file("."))
    images.add("michaelruocco/${artifactName}:latest")
}

task pushImage(type: DockerPushImage) {
    images.set(buildImage.images)
}

dockerCompose {
    startedServices = ['verification-context-app']
}
