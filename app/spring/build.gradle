buildscript {
    ext {
        springBootVersion = "2.4.1"
    }
}

plugins {
    id "org.unbroken-dome.test-sets" version "${testSetsPluginVersion}"
    id "org.springframework.boot" version "${springBootVersion}"
    id "io.spring.dependency-management" version "1.0.10.RELEASE"
    id "com.bmuschko.docker-remote-api" version "6.6.1"
    id "com.avast.gradle.docker-compose" version "0.14.0"
}

import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage

testSets {
    integrationTest
}

configurations {
    compile.exclude module: "slf4j-simple"
}

ext {
    karateVersion = "0.9.6"
    logbackJsonVersion = "0.1.5"
}

repositories {
    maven { url "https://oss.jfrog.org/artifactory/oss-snapshot-local" }
}

dependencies {
    implementation "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-hateoas"
    implementation "org.springframework.boot:spring-boot-starter-actuator"

    implementation project(":manual-app")
    implementation project(":identity-config")
    implementation project(":lockout-config")
    implementation project(":context-config")
    implementation project(":identity-json")
    implementation project(":method-config")
    implementation project(":otp-config")
    implementation project(":otp-sim-swap-stub-config")
    implementation project(":lockout-json")
    implementation project(":context-json")
    implementation project(":method-json")
    implementation project(":otp-json")
    implementation project(":identity-external-find-stub-config")
    implementation project(":data-protector")
    implementation "org.apache.commons:commons-collections4:${commonsCollectionsVersion}"
    implementation "com.github.bohnman:squiggly-filter-jackson:1.3.18"
    implementation "com.github.michaelruocco:spring-filters:0.1.8"
    implementation "commons-io:commons-io:2.8.0"
    implementation "ch.qos.logback.contrib:logback-json-classic:${logbackJsonVersion}"
    implementation "ch.qos.logback.contrib:logback-jackson:${logbackJsonVersion}"
    implementation "org.mongodb:mongo-java-driver:${mongoVersion}"
    implementation "net.logstash.logback:logstash-logback-encoder:6.5"
    implementation "com.github.michaelruocco:string-utils:${mruocStringUtilsVersion}"

    testImplementation "org.junit-pioneer:junit-pioneer:${junitPioneerVersion}"
    testImplementation testFixtures(project(":lockout-entities"))
    testImplementation testFixtures(project(":context-entities"))

    integrationTestImplementation "com.intuit.karate:karate-apache:${karateVersion}"
    integrationTestImplementation "com.intuit.karate:karate-junit5:${karateVersion}"
    integrationTestImplementation "org.awaitility:awaitility:${awaitilityVersion}"
    integrationTestImplementation "org.testcontainers:junit-jupiter:1.15.1"
}

springBoot {
    buildInfo()
}

bootRun {
    systemProperties = System.getProperties()
    systemProperty "spring.profiles.active", System.properties.getOrDefault("spring.profiles.active", "stubbed,simple-logging")
    systemProperty "server.port", System.properties.getOrDefault("server.port", "8081")
    systemProperty "request.logging.enabled", System.properties.getOrDefault("request.logging.enabled", "true")
    systemProperty "response.logging.enabled", System.properties.getOrDefault("response.logging.enabled", "true")
    systemProperty "response.filtering.enabled", System.properties.getOrDefault("response.filtering.enabled", "true")
}

integrationTest {
    systemProperty "aws.accessKeyId", "abc"
    systemProperty "aws.secretKey", "123"
}

docker {
    registryCredentials {
        username.set(System.getenv("DOCKER_USERNAME"))
        password.set(System.getenv("DOCKER_PASSWORD"))
    }
}

task buildImage(type: DockerBuildImage) {
    inputDir.set(file("."))
    images.add("michaelruocco/verification-context-spring-app:latest")
}

task pushImage(type: DockerPushImage) {
    images.set(buildImage.images)
}

dockerCompose {
    startedServices = ['verification-context-app']
}