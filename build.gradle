buildscript {
    configurations.classpath {
        resolutionStrategy {
            //this jgit version is required to fix this issue:
            //https://github.com/diffplug/spotless/issues/587 with the spotless plugin
            force 'org.eclipse.jgit:org.eclipse.jgit:5.7.0.202003110725-r'
        }
    }
}

plugins {
    id "idea"
    id "java-library"
    id "com.diffplug.spotless" version "5.11.0"
    id "com.github.ben-manes.versions" version "0.38.0"
    id "com.vanniktech.dependency.graph.generator" version "0.5.0"
    id "pl.allegro.tech.build.axion-release" version "1.12.1"
    id "org.sonarqube" version "3.1.1"
    id "nebula.lint" version "16.17.0"
    id "io.codearte.nexus-staging" version "0.30.0"
}

scmVersion {
    versionCreator "versionWithBranch"
    tag {
        prefix = "idv-context"
    }
}
project.version = scmVersion.version

allprojects {
    apply plugin: "idea"
    apply plugin: "jacoco"
    apply plugin: "nebula.lint"

    group "com.github.michaelruocco.idv"

    repositories {
        mavenCentral()
        jcenter()
    }

    jacoco {
        toolVersion = "0.8.6"
    }

    gradleLint {
        rules = ['all-dependency']
        alwaysRun = false
    }
}

subprojects {
    apply plugin: "java-library"
    apply plugin: "com.diffplug.spotless"

    version = rootProject.version

    sourceCompatibility = 13
    targetCompatibility = 13

    ext {
        testSetsPluginVersion = "3.0.1"
        shadowPluginVersion = "6.1.0"

        lombokVersion = "1.18.18"
        slf4jVersion = "1.7.30"
        commonsCollectionsVersion = "4.4"
        jacksonVersion = "2.12.2"
        jsonAdapterVersion = "1.0.0"
        mongoVersion = "3.12.8"
        mruocStringUtilsVersion = "0.1.4"
        systemStubsVersion = "1.2.0"
        commonsLangVersion = "3.12.0"
        neovisionariesVersion = "1.27"
        redissionVersion = "3.15.1"
        jsonPathVersion = "2.5.0"
        jsonMaskerVersion = "1.0.6"
        moneyApiVersion = "1.1"
        monetaCoreVersion = "1.4.2"
        commonsMathVersion = "3.6.1"
        durationLoggerVersion = "0.1.0"
        randomValueSuppliersVersion = "0.1.0"

        junitVersion = "5.7.1"
        assertjVersion = "3.19.0"
        junitPioneerVersion = "1.3.8"
        mockitoVersion = "3.8.0"
        jsonUnitAssertjVersion = "2.24.0"
        fileLoaderVersion = "6.0.1"
        awaitilityVersion = "4.0.3"
        testingClocksVersion = "0.1.4"

        gitUrl = "https://github.com/michaelruocco/idv-context"
        pomConfig = {
            licenses {
                license {
                    name "MIT License"
                    url "https://opensource.org/licenses/MIT"
                    distribution "repo"
                }
            }

            developers {
                developer {
                    id "mruoc"
                    name "Michael Ruocco"
                    email "michael.ruocco@hotmail.com"
                }
            }

            scm {
                url project.ext.gitUrl
            }
        }
    }

    dependencies {
        compileOnly "org.projectlombok:lombok:${lombokVersion}"
        annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
        testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
        testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

        implementation "org.slf4j:slf4j-api:${slf4jVersion}"

        testRuntimeOnly "org.slf4j:slf4j-simple:${slf4jVersion}"
        testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
        testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
        testImplementation "org.assertj:assertj-core:${assertjVersion}"
        testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    }

    tasks.withType(Test) {
        useJUnitPlatform()
        systemProperty "org.slf4j.simpleLogger.defaultLogLevel", "debug"
        testLogging {
            events "failed"
            exceptionFormat "full"
            showStandardStreams true
        }
    }

    spotless {
        java {
            removeUnusedImports()
            trimTrailingWhitespace()
            indentWithSpaces(4)
            endWithNewline()

            replace 'Not enough space after if', 'if(', 'if ('
            replaceRegex 'Too much space after if', 'if +\\(', 'if ('

            replace 'Not enough space after for', 'for(', 'for ('
            replaceRegex 'Too much space after for', 'for +\\(', 'for ('

            replace 'Not enough space after while', 'while(', 'while ('
            replaceRegex 'Too much space after while', 'while +\\(', 'while ('
        }
    }
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.enabled false
        csv.enabled false
    }
}

project.tasks["sonarqube"].dependsOn codeCoverageReport
sonarqube {
    properties {
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.organization", "michaelruocco"
        property "sonar.projectKey", "michaelruocco_idv-context"
        property "sonar.coverage.jacoco.xmlReportPaths", "${project.buildDir}/reports/jacoco/report.xml"
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
    subprojects.findAll { it.tasks.findByName("integrationTest") != null }.integrationTest
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ["alpha", "beta", "rc", "cr", "m", "preview"].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject("Release candidate")
            }
        }
    }
}

import com.vanniktech.dependency.graph.generator.DependencyGraphGeneratorExtension.Generator

def idvContextGenerator = new Generator(
        "idvContext",
        { dependency -> dependency.getModuleGroup().startsWith("com.github.michaelruocco.idv") },
        { dependency -> true }
)

dependencyGraphGenerator {
    generators = [Generator.ALL, idvContextGenerator]
}