plugins {
    id "idea"
    id "java-library"
    id "com.diffplug.spotless" version "6.0.3"
    id "com.github.ben-manes.versions" version "0.39.0"
    id "pl.allegro.tech.build.axion-release" version "1.13.6"
    id "org.sonarqube" version "3.3"
    id "nebula.lint" version "17.5.0"
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
    id "com.adarshr.test-logger" version "3.1.0"
}

scmVersion {
    versionCreator "versionWithBranch"
    tag {
        prefix = "idv-context-"
    }
}
project.version = scmVersion.version

allprojects {
    apply plugin: "idea"
    apply plugin: "jacoco"
    apply plugin: "nebula.lint"

    group "com.github.michaelruocco.idv"
    version = rootProject.version

    sourceCompatibility = 15
    targetCompatibility = 15

    repositories {
        mavenCentral()
        jcenter()
    }

    jacoco {
        toolVersion = "0.8.7"
    }

    gradleLint {
        rules = ['all-dependency']
        alwaysRun = false
    }
}

subprojects {
    apply plugin: "java-library"
    apply plugin: "com.diffplug.spotless"
    apply plugin: 'com.adarshr.test-logger'

    ext {
        testSetsPluginVersion = "4.0.0"
        shadowPluginVersion = "6.1.0"

        lombokVersion = "1.18.22"
        slf4jVersion = "1.7.32"
        commonsCollectionsVersion = "4.4"
        jacksonVersion = "2.13.0"
        jsonAdapterVersion = "1.0.4"
        mongoVersion = "3.12.10"
        mruocStringUtilsVersion = "0.1.6"
        systemStubsVersion = "1.2.0"
        commonsLangVersion = "3.12.0"
        neovisionariesVersion = "1.29"
        redissonVersion = "3.16.6"
        jsonPathVersion = "2.6.0"
        jsonMaskerVersion = "1.0.7"
        moneyApiVersion = "1.1"
        monetaCoreVersion = "1.4.2"
        commonsMathVersion = "3.6.1"
        durationLoggerVersion = "0.1.0"
        randomValueSuppliersVersion = "0.1.0"
        logbackVersion = "1.2.7"

        junitVersion = "5.8.2"
        assertjVersion = "3.21.0"
        junitPioneerVersion = "1.5.0"
        mockitoVersion = "4.1.0"
        jsonUnitAssertjVersion = "2.28.0"
        fileLoaderVersion = "6.0.2"
        awaitilityVersion = "4.1.1"
        testingClocksVersion = "0.1.4"

        gitUrl = "https://github.com/michaelruocco/idv-context"
        pomConfig = {
            licenses {
                license {
                    name "MIT License"
                    url "https://opensource.org/licenses/MIT"
                    distribution "repo"
                }
            }

            developers {
                developer {
                    id "mruoc"
                    name "Michael Ruocco"
                    email "michael.ruocco@hotmail.com"
                }
            }

            scm {
                url project.ext.gitUrl
            }
        }
    }

    dependencies {
        compileOnly "org.projectlombok:lombok:${lombokVersion}"
        annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
        testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
        testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

        implementation "org.slf4j:slf4j-api:${slf4jVersion}"

        testRuntimeOnly "org.slf4j:slf4j-simple:${slf4jVersion}"
        testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
        testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
        testImplementation "org.assertj:assertj-core:${assertjVersion}"
        testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    }

    javadoc.options.addStringOption('Xdoclint:none', '-quiet')

    tasks.withType(Test) {
        useJUnitPlatform()
        systemProperty "org.slf4j.simpleLogger.defaultLogLevel", "debug"
    }

    testlogger {
        theme 'mocha'
        showStandardStreams true
    }

    spotless {
        java {
            removeUnusedImports()
            trimTrailingWhitespace()
            indentWithSpaces(4)
            endWithNewline()

            replace 'Not enough space after if', 'if(', 'if ('
            replaceRegex 'Too much space after if', 'if +\\(', 'if ('

            replace 'Not enough space after for', 'for(', 'for ('
            replaceRegex 'Too much space after for', 'for +\\(', 'for ('

            replace 'Not enough space after while', 'while(', 'while ('
            replaceRegex 'Too much space after while', 'while +\\(', 'while ('
        }
    }
}

nexusPublishing {
    packageGroup = "com.github.michaelruocco"
    repositories {
        sonatype {
            username = System.getenv("OSSRH_USERNAME")
            password = System.getenv("OSSRH_PASSWORD")
        }
    }
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.enabled false
        csv.enabled false
    }
}

project.tasks["sonarqube"].dependsOn codeCoverageReport
sonarqube {
    properties {
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.organization", "michaelruocco"
        property "sonar.projectKey", "michaelruocco_idv-context"
        property "sonar.coverage.jacoco.xmlReportPaths", "${project.buildDir}/reports/jacoco/report.xml"
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
    subprojects.findAll { it.tasks.findByName("integrationTest") != null }.integrationTest
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ["alpha", "beta", "rc", "cr", "m", "preview"].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject("Release candidate")
            }
        }
    }
}