buildscript {
    configurations.classpath {
        resolutionStrategy {
            //this jgit version is required to fix this issue:
            //https://github.com/diffplug/spotless/issues/587 with the spotless plugin
            force 'org.eclipse.jgit:org.eclipse.jgit:5.7.0.202003110725-r'
        }
    }
}

plugins {
    id "idea"
    id "java-library"
    id "com.diffplug.spotless" version "5.9.0"
    id "com.github.ben-manes.versions" version "0.36.0"
    id "com.vanniktech.dependency.graph.generator" version "0.5.0"
    id "pl.allegro.tech.build.axion-release" version "1.12.1"
    id "org.sonarqube" version "3.1"
    id "nebula.lint" version "16.17.0"
    id "com.jfrog.bintray" version "1.8.5"
    id "com.jfrog.artifactory" version "4.18.3"
}

scmVersion {
    versionCreator "versionWithBranch"
    tag {
        prefix = "verification-context"
    }
}
project.version = scmVersion.version

allprojects {
    apply plugin: "idea"
    apply plugin: "jacoco"
    apply plugin: "nebula.lint"

    group "com.github.michaelruocco"

    repositories {
        mavenCentral()
        jcenter()
    }

    jacoco {
        toolVersion = "0.8.6"
    }

    gradleLint {
        rules = ['all-dependency']
        alwaysRun = false
    }
}

subprojects {
    apply plugin: "java-library"
    apply plugin: "com.diffplug.spotless"

    version = rootProject.version
    archivesBaseName = "${rootProject.name}-${project.name}"

    sourceCompatibility = 13
    targetCompatibility = 13

    ext {
        testSetsPluginVersion = "3.0.1"
        shadowPluginVersion = "6.1.0"

        lombokVersion = "1.18.16"
        slf4jVersion = "1.7.30"
        commonsCollectionsVersion = "4.4"
        awsSdkVersion = "1.11.895"
        jacksonVersion = "2.12.1"
        jsonAdapterVersion = "1.0.0"
        mongoVersion = "3.12.7"
        mruocStringUtilsVersion = "0.1.2"
        systemLambdaVersion = "1.2.0"
        commonsLangVersion = "3.11"
        neovisionariesVersion = "1.27"
        redissionVersion = "3.14.1"
        jsonPathVersion = "2.5.0"
        jsonMaskerVersion = "1.0.6"
        moneyApiVersion = "1.1"
        monetaCoreVersion = "1.4.2"
        commonsMathVersion = "3.6.1"

        junitVersion = "5.7.0"
        assertjVersion = "3.18.1"
        junitPioneerVersion = "1.2.0"
        mockitoVersion = "3.7.7"
        jsonUnitAssertjVersion = "2.22.1"
        fileLoaderVersion = "6.0.1"
        awaitilityVersion = "4.0.3"
        testingClocksVersion = "0.1.4"
    }

    dependencies {
        compileOnly "org.projectlombok:lombok:${lombokVersion}"
        annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
        testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
        testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

        implementation "org.slf4j:slf4j-api:${slf4jVersion}"

        testRuntimeOnly "org.slf4j:slf4j-simple:${slf4jVersion}"
        testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
        testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
        testImplementation "org.assertj:assertj-core:${assertjVersion}"
        testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    }

    tasks.withType(Test) {
        useJUnitPlatform()
        systemProperty "org.slf4j.simpleLogger.defaultLogLevel", "debug"
        testLogging {
            events "failed"
            exceptionFormat "full"
            showStandardStreams true
        }
    }

    spotless {
        java {
            removeUnusedImports()
            trimTrailingWhitespace()
            indentWithSpaces(4)
            endWithNewline()

            replace 'Not enough space after if', 'if(', 'if ('
            replaceRegex 'Too much space after if', 'if +\\(', 'if ('

            replace 'Not enough space after for', 'for(', 'for ('
            replaceRegex 'Too much space after for', 'for +\\(', 'for ('

            replace 'Not enough space after while', 'while(', 'while ('
            replaceRegex 'Too much space after while', 'while +\\(', 'while ('
        }
    }
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.enabled false
        csv.enabled false
    }
}

project.tasks["sonarqube"].dependsOn codeCoverageReport
sonarqube {
    properties {
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.organization", "michaelruocco"
        property "sonar.projectKey", "michaelruocco_verification-context"
        property "sonar.coverage.jacoco.xmlReportPaths", "${project.buildDir}/reports/jacoco/report.xml"
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
    subprojects.findAll { it.tasks.findByName("integrationTest") != null }.integrationTest
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ["alpha", "beta", "rc", "cr", "m", "preview"].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject("Release candidate")
            }
        }
    }
}

import com.vanniktech.dependency.graph.generator.DependencyGraphGeneratorExtension.Generator

def verificationContextGenerator = new Generator(
        "verificationContext",
        { dependency -> dependency.getModuleGroup().startsWith("com.github.michaelruocco") },
        { dependency -> true }
)

dependencyGraphGenerator {
    generators = [Generator.ALL, verificationContextGenerator]
}
